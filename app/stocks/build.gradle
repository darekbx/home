plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.7.20"
}

apply from: '../../dependencies.gradle'

android {
    namespace 'com.darekbx.stocks'
    compileSdk 34

    defaultConfig {
        minSdk 30
        targetSdk 33

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    buildTypes.all { type ->
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def currenciesBaseUrl = properties.getProperty('currencies.baseUrl')
        buildConfigField 'String', 'CURRENCIES_BASE_URL', "\"${currenciesBaseUrl}\""
    }
}

useCompose()
useGlance()
useHilt()

dependencies {

    implementation project(':app:common')
    implementation project(':app:storage')

    implementation other.kotlinx_serialization

    implementation other.retrofit
    implementation other.retrofit_scalars
    implementation other.okhttp
    implementation other.okhttp_logging_interceptor

    implementation androidx.core_ktx
    implementation androidx.lifecycle_runtime_ktx
    implementation androidx.compose_navigation

    testImplementation other.junit
}